apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    signingConfigs {
        release {
            def credsFilePath = file("../credentials.json").toString()
            def credsFile = new File(credsFilePath, "").getText('UTF-8')
            def json = new groovy.json.JsonSlurper().parseText(credsFile)
            storeFile file(json.android.storeFile)
            storePassword = json.android.storePassword
            keyAlias = json.android.keyAlias
            keyPassword = json.android.keyPassword
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "ru.vincetti.vimoney"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 34
        versionName "5.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinx_coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinx_coroutines}"

    implementation "androidx.annotation:annotation:${versions.androidx_annotation}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.androidx_constraintlayout}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidx_recyclerview}"
    implementation "androidx.core:core-ktx:${versions.androidx_core_ktx}"
    implementation "androidx.fragment:fragment-ktx:${versions.androidx_fragment_ktx}"
    implementation "androidx.paging:paging-runtime-ktx:${versions.androidx_paging}"
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.androidx_navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.androidx_navigation}"
    implementation "androidx.lifecycle:lifecycle-extensions:${versions.androidx_lifecycle}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.androidx_lifecycle}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${versions.androidx_lifecycle}"
    implementation "com.google.android.material:material:${versions.material}"

    implementation "com.squareup.picasso:picasso:${versions.picasso}"

    implementation "androidx.room:room-runtime:${versions.room}"
    implementation "androidx.room:room-ktx:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"

    implementation "com.github.QuadFlask:colorpicker:${versions.colorpicker}"
    implementation "com.diogobernardino:williamchart:${versions.williamchart}"

    implementation "com.google.firebase:firebase-analytics:${versions.firebase_analytics}"
    implementation "com.google.firebase:firebase-crashlytics:${versions.firebase_crashlytics}"

    kapt "com.google.dagger:hilt-android-compiler:${versions.gradle_hilt}"
    implementation "com.google.dagger:hilt-android:${versions.gradle_hilt}"

    implementation project(':Core')
    implementation project(':Files')
    implementation project(':Database')
    implementation project(':Network')

    //Test
    implementation "androidx.test:core:${versions.androidx_test_core}"
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "io.mockk:mockk:${versions.mockk}"
    androidTestImplementation "androidx.test.ext:junit:${versions.androidx_test_junit}"
    androidTestImplementation "com.kaspersky.android-components:kaspresso:${versions.kaspresso}"
}
